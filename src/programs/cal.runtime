/displays the calendar of the current month
let months []
psh $months 'Jan' 'Feb' 'Mar' 'Apr'
psh $months 'May' 'Jun' 'Jul' 'Aug'
psh $months 'Sep' 'Oct' 'Nov' 'Dec'
let month_days []
psh $month_days 31 28 31 30 31 30 31 31 30 31 30 31
tim _day day
tim _month month
tim _date date
tim _year year
get $months $_month _month_str
prt '      ' ''
prt $_month_str ' '
prt $_year
get $month_days $_month day_count
jne $_month 1 check_leap_done
mod res $_year 400
jeq $res 0 leap_year
mod res $_year 100
jeq $res 0 check_leap_done
mod res $_year 4
jeq $res 0 leap_year
jmp check_leap_done
#leap_year
let day_count 29
#check_leap_done
prt 'Su Mo Tu We Th Fr Sa'
let i 0
let column 0
let shift $_day
mod back_off $_date 7
sub shift $shift $back_off
jlt $shift 0 add_seven
jmp adjust_shift
#add_seven
add shift $shift 7
#adjust_shift
add shift $shift 1
mod shift $shift 7
#skip_cell
jeq $i $shift start
prt '   ' ''
add i $i 1
add column $column 1
jmp skip_cell
#start
let i 1
#loop
jgt $i $day_count done
jgt $i 9 print_day
prt ' ' ''
#print_day
prt $i ' '
add i $i 1
add column $column 1
jne $column 7 loop
prt ''
let column 0
jmp loop
#done
prt ''