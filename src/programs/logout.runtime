/logout from remote server
let AUTH_API 'https://siwei.dev'
add API_SESSION $AUTH_API '/session'
add API_LOGOUT $AUTH_API '/logout?redirect=false'
add API_SAVE_FILE $AUTH_API '/api/fs/save'

def build_req
 let _method $0
 let _url $1
 let _data $2
 let _with_credential $3
 let _req {}
 put $_req 'method' $_method
 put $_req 'url' $_url
 jeq $_data $nil skip_data
  put $_req 'data' $_data
 #skip_data
 ife $_with_credential 1
  put $_req 'with_credential' 1
 fin
 ret $_req
end

/ check existing session
cal build_req 'POST' $API_SESSION $nil 1
lib 'net.http' $ret res
get $res 'status' status
jeq $status 1 error_not_logged_in
get $res 'username' session_username
lib 'os.get_env_var' 'user' current_user
jne $session_username $current_user error_login_status

/ save files
prt 'Saving files...'
let home_dir []
psh $home_dir 'home'
lib 'os.read_file' $home_dir $current_user user_files
str user_files_str $user_files
let payload {}
put $payload 'data' $user_files_str
cal build_req 'POST' $API_SAVE_FILE $payload 1
lib 'net.http' $ret res
get $res 'status' status
jne $status 0 error_save_file

/ logout session
prt 'Logging out...'
cal build_req 'GET' $API_LOGOUT $nil 1
lib 'net.http' $ret _

/ switch to guest
prt 'Switching to guest account...'
lib 'os.get_home_path' _home_path
lib 'os.delete_path' $_home_path
lib 'os.set_env_var' 'user' 'guest'
lib 'os.get_home_path' _home_path
lib 'os.set_current_path' $_home_path

prt 'Logged out'
jmp end

#error_save_file
prt 'ERR failed to save files, logout aborted'
jmp end

#error_login_status
prt 'ERR invalid login status, please login first'
jmp end

#error_not_logged_in
prt 'ERR not logged in'
jmp end

#end
