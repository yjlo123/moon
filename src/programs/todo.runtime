/manage a todo list
lib 'os.get_home_path' home_path

lib 'term.get_height' h
jeq $h $nil err_term_not_supported
sub h $h 1

lib 'term.set_prt_delay' 0
lib 'term.alternate_buffer'

def print_header
 lib 'term.clear_screen'
 prt '┌────────── TODO v1.0 ───────────┐'
 prt '│Usage: add <t> / done <i> / exit│'
 prt '└────────────────────────────────┘'
end

cal print_header

#load_data
lib 'os.read_file' $home_path '_todo' file_data
jne $file_data $nil file_loaded
lib 'os.write_file' $home_path '_todo' []
jmp load_data
#file_loaded
let todo $file_data
len $todo size

/ initial print
let i 0
#print_todo
jeq $i $size print_done
add j $i 1
prt $j '. '
get $todo $i item
prt $item
add i $i 1
jmp print_todo
#print_done

/ === MAIN LOOP
#main_loop
prt '>' ''
inp input

let cmd ''
#parse
pol $input c
jeq $c '' parse_done
jeq $c ' ' parse_done
psh $cmd $c
jmp parse
#parse_done

jeq $cmd 'exit' end

let ri -1

jne $cmd 'add' not_add
psh $todo $input
len $todo size
let ri 0
#not_add

jne $cmd 'done' not_done
int idx $input
sub idx $idx 1
put $todo $idx ''
let new_todo []
#update_done
pol $todo t
jeq $t $nil update_done_finish
jeq $t '' update_done
psh $new_todo $t
jmp update_done
#update_done_finish
let todo $new_todo
#not_done

/ clear screen
cal print_header

len $todo size

let i 0
#reprint
jeq $i $size reprint_done
add j $i 1
prt $j '. '
get $todo $i item
prt $item
add i $i 1
jmp reprint
#reprint_done

lib 'term.clear_line'
let ri -1
jmp main_loop
/ ^^^ end of MAIN LOOP

#err_term_not_supported
prt 'ERR Your current terminal is not supported'
jmp exit

#end
lib 'os.write_file' $home_path '_todo' $todo
lib 'term.clear_screen'
lib 'term.primary_buffer'
/prt 'Bye~'

#exit
lib 'term.set_prt_delay' 1